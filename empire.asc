0 'COPYRIGHT (C) CLOAD 1981
1 ' - EMPIRE -
3 REM PETER TREFONAS 12/30/79
5 'CREDITS:"Hammurabi" BY RICK MERRIL & DAVID AHL,"Santa Paravia" BY GEORGE BLANK
6 CLS:INPUT"Please hit <enter> to delete lines 0-6 for memory conservation. Then 'RUN' program again";A$:DELETE0-6
7 CLS:PRINTTAB(20);"E M P I R E":PRINT@448,"(Always hit <enter> to continue)":GOSUB59:CLS
9 CLEAR110:DEFSTRZ:DEFINTO:RANDOM:DIMA(6,19),Z(6,6)
11 FORI=1TO6:FORJ=0TO6:READZ(I,J):NEXT:NEXTI:BA=6000
13 FORI=1TO6:A(I,1)=10000:A(I,2)=15000+RND(10000):A(I,3)=2000:A(I,4)=1000:A(I,7)=25:A(I,8)=20:A(I,9)=5:A(I,10)=35:A(I,15)=20:A(I,17)=2:A(I,18)=1:A(I,19)=15:NEXT
15 INPUT"How many people are playing";NP:IFNP>6THEN15
17 FORI=1TONP:PRINT"Who is the ruler of ";Z(I,1);:INPUTZ(I,0):NEXT
19 NY=NY+1:CLS:PRINT"Year";NY:PRINT:NW=RND(6):ONNWGOTO21,23,25,27,29,31
21 PRINT"Poor weather. No rain. Locusts migrate.":GOTO35
23 PRINT"Early frosts. Arid conditions.":GOTO35
25 PRINT"Flash floods. Too much rain.":GOTO35
27 PRINT"Average weather. Good year.":GOTO35
29 PRINT"Fine weather. Long summer.":GOTO35
31 PRINT"Fantastic weather ! Great year !":GOTO35
33 '
35 GOSUB59:CLS:FORK=1TO6:QF=0
37 IFK>NPGOSUB411:GOTO41
39 GOSUB65
41 NEXT:CLS
43 PRINT"Summary"
45 PRINT"Nobles   Soldiers   Merchants   Serfs   Land    Palace":PRINT
47 FORI=1TO6:IFA(I,0)<>0THEN53ELSEPRINTZ(I,A(I,17));" ";Z(I,0);" of ";Z(I,1)
49 Z=" ###      ##,###      ##,###   ###,###  ##,###  ###%"
51 PRINTUSINGZ;A(I,18),A(I,15),A(I,7),A(I,3),A(I,1),A(I,16)*10
53 IFKK=1END
55 NEXT:INPUT"<Enter>";Z:GOTO19
57 PRINT@896,CHR$(30):PRINT@960,CHR$(30);:PRINT@896,"";:RETURN
59 FORII=1TO1600:NEXT:RETURN
61 PRINT@128,CHR$(30):PRINT@128,A(K,8);"%";,A(K,9);"%",A(K,10);"%":PRINTINT(FC+.5),INT(FS+.5),INT(FI+.5):RETURN
63 CLS:PRINT"Very sad news ...":PRINT:RETURN
65 '
67 GOTO395
69 DS=0:IQ=1:LA=A(K,1)-A(K,3)-A(K,18)*2-A(K,16)-A(K,7)-A(K,15)*2:PD=(A(K,3)+A(K,7)+A(K,18)*3)*5:AD=A(K,15)*8:A(K,19)=15
71 GOTO359
73 IFA(K,2)*3<LALETLA=A(K,2)*3
75 IFA(K,3)*5<LALETLA=A(K,3)*5
77 A(K,2)=A(K,2)-LA/3:HA=LA*NW*.72+RND(500)-A(K,13)*500:RA=RND(30):A(K,2)=A(K,2)-A(K,2)*RA/100:IFHA<0LETHA=0
79 A(K,2)=A(K,2)+HA
81 CLS:PRINTZ(K,A(K,17));" ";Z(K,0);" of ";Z(K,1)
83 PRINT"Rats ate";RA;"% of the grain reserve"
85 PRINT"Grain	     Grain     People     Army       Royal
87 PRINT"harvest   reserve   require    requires   treasury
89 LETZ="###,###   ###,###   ###,###    ###,###    ###,###":PRINTUSINGZ;HA,A(K,2),PD,AD,A(K,4)
91 PRINT"bushels   bushels   bushels    bushels    ";Z(K,6)
93 PRINT@384,"";:PRINT"------Grain for sale:":PRINT,"Country","Bushels","Price":J=0
95 FORI=1TO6:IFA(I,0)<>0ORA(I,5)=0THEN99
97 PRINTI,Z(I,1),A(I,5),:PRINTUSING"##.##";A(I,6):J=1
99 NEXTI:IFJ=0PRINT:PRINT:PRINT"No grain for sale . . .":PRINT
101 H=0:GOSUB57:INPUT"1) Buy grain  2) Sell grain  3) Sell land";H:IFH>3ORH<0GOTO101
103 ONH+1GOTO141,105,121,133
105 '
107 GOSUB57:INPUT"From which country  (give #)";H1:IFH1<0ORH1>6GOTO107ELSEIFA(H1,0)=1ORA(H1,5)=0PRINT"That country has none for sale!";:GOSUB59:GOTO101
109 IFH1=0GOTO101
111 IFH1=KGOSUB57:PRINT"You cannot buy grain that you have put onto the market!";:GOSUB59:GOTO107
113 GOSUB57:INPUT"How many bushels";H2:IFH2>A(H1,5)PRINT"You can't buy more grain then they are selling!";:GOSUB59:GOTO113
115 IFH2<0GOTO107
117 H3=H2*A(H1,6)/.9:IFH3>A(K,4)GOSUB57:PRINTZ(K,A(K,17));" ";Z(K,0);" please reconsider -":H=A(K,4)*.9/A(H1,6):PRINT"You can only afford to buy";INT(H);"bushels";:GOSUB59:GOTO113
119 A(K,2)=A(K,2)+H2:A(K,4)=A(K,4)-H3:A(H1,4)=A(H1,4)+H3*.9:A(H1,5)=A(H1,5)-H2:GOTO81
121 '
123 GOSUB57:INPUT"How many bushels do you wish to sell";H1:IFH1>A(K,2)GOSUB57:PRINTZ(K,A(K,17));" ";Z(K,0);", please think again":PRINT"You only have ";A(K,2);"bushels.";:GOSUB59:GOTO123
125 IFH1<0GOTO123
127 GOSUB57:INPUT"What will be the price per bushel";H2:IFH2<=0GOTO127
129 IFH2>15PRINT"Be reasonable . . .even gold costs less than that!";:GOSUB59:GOTO127
131 A(K,6)=(A(K,6)*A(K,5)+H1*H2)/(A(K,5)+H1):A(K,5)=A(K,5)+H1:A(K,2)=A(K,2)-H1:GOTO81
133 'SELL LAND
135 GOSUB57:PRINT"The barbarians will give you 2 ";Z(K,6);" per acre":GOSUB59:GOSUB57:INPUT"How many acres will you sell them";H1:IFH1>A(K,1)*.95PRINT"You must keep some land for the royal palace!";:GOSUB59:GOTO135
137 IFH1<0GOTO135
139 A(K,4)=A(K,4)+H1*2:A(K,1)=A(K,1)-H1:BA=BA+H1:GOTO81
141 '
143 GOSUB57:PRINT"How many bushels will you give to your army of";A(K,15);"men";:INPUTHM:IFHM>A(K,2)GOSUB57:PRINT"You cannot give your army more grain than you have!";:GOSUB59:GOTO143
145 A(K,2)=A(K,2)-HM:PO=A(K,3)+A(K,7)+A(K,18)
147 GOSUB57:PRINT"How many bushels will you give to your";INT(PO+.5);"people";:INPUTHS:IFHS>A(K,2)GOSUB57:PRINT"But you only have";A(K,2);"bushels of grain!":GOSUB59:GOTO147
149 IFHS<A(K,2)*.1GOSUB57:PRINT"You must release at least 10% of the stored grain":GOSUB59:GOTO147
151 A(K,2)=A(K,2)-HS:A(K,19)=HM/(AD+.001)*10:IFA(K,19)<5LETA(K,19)=5
153 IFA(K,19)>15LETA(K,19)=15
155 CLS
157 PRINTZ(K,A(K,17));" ";Z(K,0);" of ";Z(K,1);":":PRINT"In Year";STR$(NY);","
159 DB=RND(PO/9.5):DM=0:DD=RND(PO/22):DE=0:IFHS>PD*1.5LETD=SQR(HS-PD)-RND(A(K,8)*1.5):IFD<0THEN161ELSEDE=RND(2*D+1)
161 DS=0:IFPD>HS*2LETDS=RND(PO/16+1):DM=RND(PO/12+1):GOTO165
163 IFPD>HSLETDM=RND(PO/15+1)
165 PT=DB-DS-DM-DD+DE:I1=RND(DE/5):I2=RND(DE/25):A(K,7)=A(K,7)+I1:A(K,18)=A(K,18)+I2:A(K,3)=A(K,3)+PT-I1-I2
167 DA=0:IFAD>HM*2LETDA=RND(A(K,15)/2+1):A(K,15)=A(K,15)-DA
169 PA=0:IFHM*2<ADLETPA=RND(A(K,15)/5):A(K,15)=A(K,15)-PA
171 PRINT:PRINTDB;"babies were born":PRINTDD;"people died of disease":IFDE>0PRINTDE;"people immigrated into your country."
173 IFDM>0PRINTDM;"people died of malnutrition."
175 IFDS>0PRINTDS;"people starved to death."
177 IFDA>0PRINTDA;"soldiers starved to death."
179 PRINT"Your army will fight at";INT(A(K,19)*10+.5);"% effeciency."
181 Z="gained":IFPT<0LETZ="lost"
183 PRINT"Your population ";Z;ABS(PT);"citizens."
185 PRINT:PRINT:INPUT"<Enter>";Z
187 GOTO373
189 '
191 F1=(A(K,11)*((A(K,7)+RND(35)+RND(35))/(A(K,9)+1)*12+5))[.9:F2=(A(K,12)*(5.8*(HA+RND(250))/(A(K,10)*20+A(K,9)*40+10)+150))[.9:F3=(A(K,13)*(A(K,15)+RND(150)+400))[.9
193 F4=(A(K,14)*(A(K,7)*4+A(K,11)*9+A(K,13)*15)*NW)[.9:F5=A(K,15)*(-8):FC=DE*(RND(40)+RND(40))/100*A(K,8):FS=A(K,9)/100*((A(K,7)*1.8+F1*33+F2*17+F3*50+F4*70)[.85+A(K,18)*5+A(K,3))
195 FI=(A(K,10)/100*(A(K,3)*1.3+A(K,18)*145+A(K,7)*39+A(K,11)*99+A(K,12)*99+A(K,13)*425+A(K,14)*965))[.97
197 A(K,4)=A(K,4)+F1+F2+F3+F4+F5+FC+FS+FI
199 A(K,18)=INT(A(K,18)):CLS:PRINTUSING"State revenues:    Treasury=###,###.##";A(K,4);:PRINT" ";Z(K,6)
201 PRINT"Customs duty","Sales tax","Income tax":GOSUB61
203 PRINT:PRINT"Investments","Number","Profits","Cost"
205 PRINT"1) Marketplaces",A(K,11),INT(F1+.5),"1000"
207 PRINT"2) Grain mills",A(K,12),INT(F2+.5),"2000"
209 PRINT"3) Foundries",A(K,13),INT(F3+.5),"7000"
211 PRINT"4) Shipyards",A(K,14),INT(F4+.5),"8000"
213 PRINT"5) Soldiers",A(K,15),INT(F5+.5),"8"
215 PRINT"6) Palace",STR$(A(K,16)*10);"% Completed",,"5000":IFKL=1KL=0:RETURN
217 E=0:GOSUB57:INPUT"1) Customs duty  2) Sales tax  3) Income tax";E
219 IFE>3ORE<0GOTO217
221 ONE+1GOTO229,223,225,227
223 GOSUB57:INPUT"Give new customs tax (max=50%)";E:IFE<0ORE>50GOTO223ELSEA(K,8)=E:GOSUB61:GOTO217
225 GOSUB57:INPUT"Give new sales tax (max=20%)";E:IFE<0ORE>20GOTO225ELSEA(K,9)=E:GOSUB61:GOTO217
227 GOSUB57:INPUT"Give new income tax (max=35%)";E:IFE<0ORE>35GOTO227ELSEA(K,10)=E:GOSUB61:GOTO217
229 E=0:GOSUB57:INPUT"Any new investments (give #)";E:IFE<0ORE>6GOTO229
231 J=0:H=0:ONE+1GOTO257,233,235,237,239,241,243
233 E1=1000:J=RND(7):GOTO245
235 E1=2000:GOTO245
237 E1=7000:GOTO245
239 E1=8000:GOTO245
241 E1=8:GOTO245
243 E1=5000:H=RND(4)
245 E2=0:GOSUB57:INPUT"How many";E2:IFE2<>INT(E2)ORE2<0GOTO245ELSEIFE2=0THEN255ELSEIFA(K,4)<E1*E2GOSUB57:PRINT"Think again . . .You only have";A(K,4);Z(K,6):GOSUB59:GOTO245
247 IFE2+(E2-1)*(J+H)>A(K,3)GOSUB57:PRINT"You don't have enough serfs to train":GOSUB59:GOTO245
249 IFE1=8IF(E2+A(K,15))/PO>.05+A(K,13)*.015GOSUB57:PRINT"You cannot equip and maintain so many troops, ";Z(K,A(K,17))".";:GOSUB59:GOTO245
251 A(K,15)=INT(A(K,15)):IFE1=8ANDE2+A(K,15)>A(K,18)*20GOSUB57:PRINT"Please think again . . .  You only have";INT(A(K,18)+.5);"nobles":PRINT"to lead your troops.";:GOSUB59:GOTO245
253 IFE1=8LETA(K,3)=A(K,3)-E2ELSEA(K,7)=A(K,7)+J*E2:A(K,18)=A(K,18)+H*E2:A(K,3)=A(K,3)-(J+H)*E2
255 A(K,4)=A(K,4)-E2*E1:A(K,E+10)=A(K,E+10)+E2:KL=1:PRINT@256,"";:GOSUB203:PRINT@28,"";:PRINTUSING"###,###.##";A(K,4);:GOTO229
257 '
259 CLS:PRINT"Land holdings:":PRINT:PRINT" 1)  Barbarians",BA:IFBA<0BA=0
261 FORI=1TO6:IFA(I,0)<>0NEXT:ELSEPRINTSTR$(I+1);")  ";Z(I,1),A(I,1):NEXT
263 I=0:GOSUB57:INPUT"Who do you wish to attack (give #)";I:IFI<0ORI>7GOTO263
265 IFI=K+1PRINTZ(K,A(K,17));", Please think again.  You are #";STR$(I);"!";:GOSUB59:GOTO263
267 IFI>1ANDNY<3GOSUB57:PRINT"Due to international treaty, you cannot attack other":PRINT"nations until the third year.";:GOSUB59:GOTO263
269 IFI=1ANDBA<1GOSUB57:PRINT"All barbarian lands have been seized":BA=0:GOSUB59:GOTO263
271 IFI<>0IFA(I-1,0)<>0GOSUB57:PRINT"That player is no longer in the game":GOSUB59:GOTO263
273 IFI=0:RETURNELSEI=I-1
275 H=A(K,18)/4+1:IFIQ>HGOSUB57:PRINT"Due to a shortage of nobles , you are limited to only":PRINTINT(H);"attacks per year";:GOSUB59:GOTO263
277 GOSUB57:INPUT"How many soldiers do you wish to send";I1:IFI1<1THEN259ELSEIFI1>A(K,15)GOSUB57:PRINT"Think again... You have only";A(K,15);"soldiers":GOSUB59:GOTO277
279 IQ=IQ+1:CLS:PRINT@269,Z(K,A(K,17));" ";Z(K,0);" of ";Z(K,1);":":PRINT@169,"Soldiers remaining:";:I4=A(K,19):I5=0:I2=A(I,15):I0=A(I,1):O1=75-I1-I2:A(K,15)=A(K,15)-I1
281 IFI=0I2=RND(RND(I1)*3)+RND(RND(I1*1.5)):PRINT@333,"Pagan barbarians:";:I3=9:I0=BA:O1=75-I1-I2:GOTO287
283 PRINT@333,Z(I,A(I,17));" ";Z(I,0);" of ";Z(I,1);":":I3=A(I,19)
285 IH=0:IFA(I,15)<1PRINT@512,Z(I,1);"'s serfs are forced to defend their country!";:I2=A(I,3):I3=5:IH=1:O1=-1
287 PRINT@306,CHR$(30);:PRINT@306,INT(I1);:PRINT@370,CHR$(30);:PRINT@370,INT(I2);:FORO=0TOO1:NEXT
289 I7=INT(I1/15)+1:IFRND(I4)<RND(I3)THEN295
291 I5=I5+RND(I7*26)-RND(I7+5):I2=I2-I7:IFI5<0LETI5=0
293 GOTO299
295 I1=I1-I7
297 IFI0-I5<0GOTO341
299 IFI1>0ANDI2>0GOTO287
301 IFI1<0LETI1=0
303 IFI2<0LETI2=0
305 IFIH=1ANDI1>0GOTO341
307 CLS:PRINT@87,"Battle over":PRINT
309 IFI1>0PRINT"The forces of ";Z(K,A(K,17));" ";Z(K,0);" were victorious.":GOTO315
311 PRINTZ(K,A(K,17));" ";Z(K,0);" was defeated.":IFI5<2LETI5=0:GOTO315
313 I5=INT(I5/RND(3)):PRINT"In your defeat you nevertheless managed to capture";I5;"acres.":IFI=0THEN319ELSE317
315 PRINTI5;"acres were seized.":IFI=0GOTO319
317 IFI5>A(I,1)/3GOTO325
319 A(K,15)=I1+A(K,15):A(K,1)=A(K,1)+I5:IFI=0BA=BA-I5:IFQF=1GOSUB59:RETURN:ELSEINPUT"<Enter>";Z:GOTO259
321 IFIH=1LETIH=0:A(I,15)=0:A(I,3)=I2:A(I,1)=A(I,1)-I5:IFQF=1GOSUB59:RETURN:ELSEINPUT"<Enter>";Z:GOTO259
323 A(I,15)=I2:A(I,1)=A(I,1)-I5:IFQF=1GOSUB59:RETURN:ELSEINPUT"<Enter>";Z:GOTO259
325 IFA(I,3)>0I6=RND(A(I,3)):PRINTI6;"enemy serfs were beaten and murdered by your troops!":A(I,3)=A(I,3)-I6
327 IFA(I,11)>0I6=RND(A(I,11)):PRINTI6;"enemy marketplaces were destroyed":A(I,11)=A(I,11)-I6
329 IFA(I,2)>0I6=RND(A(I,2)):PRINTI6;"bushels of enemy grain were burned":A(I,2)=A(I,2)-I6
331 IFA(I,12)>0LETI6=RND(A(I,12)):PRINTI6;"enemy grain mills were sabotaged":A(I,12)=A(I,12)-I6
333 IFA(I,13)>0I6=RND(A(I,13)):PRINTI6;"enemy foundries were leveled":A(I,13)=A(I,13)-I6
335 IFA(I,14)>0I6=RND(A(I,14)):PRINTI6;"enemy shipyards were over-run":A(I,14)=A(I,14)-I6
337 IFA(I,18)>2I6=RND(A(I,18)/2):PRINTI6;"enemy nobles were summarily executed":A(I,18)=A(I,18)-I6
339 A(K,1)=A(K,1)+I5:A(I,1)=A(I,1)-I5:IFQF=1GOSUB59:RETURN:ELSEINPUT"<Enter>";Z:GOTO259
341 CLS:PRINT@87,"Battle over":PRINT
343 IFI=0PRINT"All barbarian lands have been seized":PRINT"The remaining barbarians fled":A(K,1)=A(K,1)+I5:A(K,15)=A(K,15)+I1:BA=0:IFQF=1GOSUB59:RETURN:ELSEINPUT"<Enter>";Z:GOTO259
345 PRINT"The country of ";Z(I,1);" was overun!":PRINT"All enemy nobles were summarily executed!":PRINT:PRINT
347 PRINT"The remaining enemy soldiers were imprisoned. All enemy serfs
349 PRINT"have pledged oaths of fealty to you, and should now be consid-
351 PRINT"ered to be your people too. All enemy merchants fled the coun-":PRINT"try. Unfortunately, all enemy assets were sacked and destroyed
353 PRINT"by your revengeful army in a drunken riot following the victory":PRINT"celebration."
355 IFIH=1LETIH=0:A(I,3)=I2
357 A(K,15)=I1+A(K,15):A(K,1)=A(K,1)+A(I,1):A(I,1)=0:A(I,0)=1:A(K,3)=A(K,3)+A(I,3):IFQF=1GOSUB59:RETURN:ELSEINPUT"<Enter>";:GOTO259
359 IFA(K,0)<>0RETURN
361 IFA(K,11)>7ANDA(K,12)>3ANDA(K,16)>1ANDA(K,1)/A(K,3)>4.8ANDA(K,3)>2300ANDA(K,18)>10LETA(K,17)=3
363 IFA(K,11)>13ANDA(K,12)>5ANDA(K,13)>0ANDA(K,16)>5ANDA(K,1)/A(K,3)>5.0ANDA(K,3)>2600ANDA(K,18)>25LETA(K,17)=4
365 IFA(K,17)>3ANDA(K,16)>9ANDA(K,4)>3100ANDA(K,18)>40CLS:A(K,17)=5:PRINT"Game over . . .":PRINTZ(K,5);" ";Z(K,0);" Of ";Z(K,1);" Wins !":PRINT:PRINT:KK=1:GOSUB43:END
367 IFQF=1GOSUB59:RETURN
369 GOTO73
371 '
373 IFRND(DS)>RND(110)GOSUB63:PRINTZ(K,A(K,17));" ";Z(K,0);" of ";Z(K,1);" has been assassinated":PRINT"by a crazed mother whose child had starved to death . . .":GOTO387
375 IFRND(0)>.01GOTO189
377 ONRND(4)GOTO379,381,383,385
379 GOSUB63:PRINTZ(K,A(K,17));" ";Z(K,0);" has been assassinated by an ambitious":PRINT"noble":GOTO387
381 GOSUB63:PRINTZ(K,A(K,17));" ";Z(K,0);" has been killed from a fall during":PRINT"the annual fox-hunt.":GOTO387
383 GOSUB63:PRINTZ(K,A(K,17));" ";Z(K,0);" died of acute food poisoning.":PRINT"The royal cook was summarily executed.":GOTO387
385 GOSUB63:PRINTZ(K,A(K,17));" ";Z(K,0);" passed away this winter from a weak heart."
387 A(K,0)=1:PRINT:PRINT"The other nation-states have sent representatives to the":PRINT"funeral":GOSUB59:GOSUB59:RETURN
389 DATAMontaigne,Auveyron,Chevalier,Prince,Roi,Empereur,francs,Arthur,Brittany,Sir,Prince,King,Emperor,francs
391 DATAMunster,Bavaria,Ritter,Prinz,Konig,Kaiser,marks,Khotan,Quatara,Hasid,Caliph,Sheik,Shah,dinars
393 DATAFerdinand,Barcelona,Caballero,Principe,Rey,Emperadore,peseta,Hjodolf,Svealand,Riddare,Prins,Kung,Kejsare,krona
395 '
397 IFRND(0)>.02GOTO69
399 CLS:PRINTTAB(22);"P L A G U E  ! ! !":PRINT
401 PRINT"Black death has struck your nation.":PRINT
403 I=INT(RND(A(K,3)/2)):A(K,3)=A(K,3)-I:PRINTI;"serfs died."
405 I=INT(RND(A(K,7)/3)):A(K,7)=A(K,7)-I:PRINTI;"merchants died."
407 I=INT(RND(A(K,15)/3)):A(K,15)=A(K,15)-I:PRINTI;"soldiers died."
409 I=INT(RND(A(K,18)/3)):A(K,18)=A(K,18)-I:PRINTI;"nobles died.":GOSUB59:GOSUB59:GOTO69
411 '
413 FORQ=1TONP:IFA(Q,0)<>0NEXTQ:PRINT:END
415 IFA(K,0)<>0RETURN
417 DS=0:CLS:PRINT"One moment -- ";Z(K,A(K,17));" ";Z(K,0);"'s turn . . .":FORQ=1TO150:NEXT
419 IFRND(0)<.01GOSUB377:RETURN
421 Q2=0:Q3=0:Q4=0:Q5=0:Q6=0:Q7=0:Q8=0:Q9=0:Q0=0:QB=0:QC=0:QD=0
423 QP=0:FORQ=1TONP:IFA(Q,0)<>0GOTO429ELSEQP=QP+1
425 Q2=A(Q,3)+Q2:Q3=A(Q,5)+Q3:Q4=A(Q,6)+Q4:Q5=A(Q,4)+Q5:Q6=A(Q,7)+Q6:Q7=A(Q,11)+Q7:Q8=A(Q,12)+Q8:Q9=A(Q,13)+Q9
427 Q0=A(Q,14)+Q0:QB=A(Q,16)+QB:QC=A(Q,18)+QC:QD=A(Q,19)+QD
429 NEXT
431 Q2=Q2/QP:Q3=Q3/QP:Q4=Q4/QP:Q5=Q5/QP:Q6=Q6/QP:Q7=Q7/QP:Q8=Q8/QP:Q9=Q9/QP:Q0=Q0/QP:QB=QB/QP:QC=QC/QP:QD=QD/QP
433 Q2=INT(Q2+RND(200)-RND(200)):Q3=INT(Q3+RND(1000)-RND(1000)):Q4=Q4+RND(0)-RND(0):Q5=INT(Q5+RND(1500)-RND(1500)):Q6=INT(Q6+RND(25)-RND(25)):IFQ4<0LETQ4=0:GOTO433
435 Q7=INT(Q7+RND(4)-RND(4)):Q8=INT(Q8+RND(2)-RND(2)):IFRND(0)>.3THEN437ELSEQ9=INT(Q9+RND(2)-RND(2)):Q0=INT(Q0+RND(2)-RND(2)):IFRND(0)>.5THEN437ELSEQB=INT(QB+RND(2)-RND(2)):QC=INT(QC+RND(2)-RND(2))
437 '
439 A(K,3)=Q2:IFQ3>A(K,5)ANDRND(9)>6LETA(K,5)=Q3:A(K,6)=Q4:IFNW<3LETA(K,6)=A(K,6)+RND(0)/1.5
441 A(K,4)=Q5:IFQ6>A(K,7)LETA(K,7)=Q6
443 IFQ7>A(K,11)LETA(K,11)=Q7
445 IFQ8>A(K,12)LETA(K,12)=Q8
447 IFQ9>A(K,13)LETA(K,13)=Q9
449 IFQ0>A(K,14)LETA(K,14)=Q0
451 IFQB>A(K,16)LETA(K,16)=QB
453 IFQC>A(K,18)LETA(K,18)=QC
455 A(K,15)=10*A(K,18)+RND(10*A(K,18)):IQ=0
457 IFA(K,15)/Q2>A(K,13)*.01+.05THENA(K,15)=A(K,15)/2:GOTO457
459 A(K,19)=QD:QF=1:GOSUB359:QF=0
461 Q=RND(NP):IFA(Q,0)<>0ORQ=KGOTO461
463 IFA(Q,5)<1GOTO471
465 QR=RND(0)*A(Q,5):IFQR*A(Q,6)<A(K,4)GOTO469
467 IFRND(9)>3GOTO465ELSE471
469 A(Q,4)=A(Q,4)+INT(QR*A(Q,6)*90)/100:A(Q,5)=A(Q,5)-QR
471 IFRND(9)<2RETURN
473 IFNY<3I=0:IFBA<0RETURNELSEGOTO477
475 I=RND(6):IFI=KORA(I,0)<>0THEN475
477 QF=1:I1=RND(A(K,15)):GOSUB279:QF=0:IQ=IQ+1:IFA(K,15)>30ANDIQ<A(K,18)/4THEN471
479 RETURN
